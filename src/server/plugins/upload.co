const
  Plugin = require('../plugin')
  mkdirp = require 'mkdirp'
  fs = require 'fs'
  path = require 'path'
  request = require 'superagent'
  url = require 'url'
  temp = require 'temp'
  mv = require 'mv'
  {getSuggestedPath} = require '../futils'
  express = require 'express'

multipart = express.multipart(keepExtensions: true)

module.exports = class Upload extends Plugin
  (bus) ~>
    super(...)
    @is_enabled = false
    @random_ids = null

    bus.on 'app', @~setUpRoutes
    bus.on 'mpd', @~setMpd
    bus.on 'save_state', @~saveState
    bus.on 'restore_state', @~restoreState
    bus.on 'status_sent', @~onSendStatus
    bus.on 'socket_connect', @~onSocketConnection

  restoreState: !(state) ->
    @want_to_queue = state.want_to_queue ? []

    @is_enabled = true
    unless (@music_directory = state.mpd_conf.music_directory)?
      @is_enabled = false
      console.warn "No music directory set. Upload disabled."
      return


  saveState: !(state) ->
    state.want_to_queue = @want_to_queue
    state.status.upload_enabled = @is_enabled

  setMpd: !(@mpd) ->
    @mpd.on 'libraryupdate', @~flushWantToQueue

  onSocketConnection: !(socket) ->
    socket.on 'ImportTrackUrl', !(url_string) ~>
      parsed_url = url.parse(url_string)
      remote_filename = path.basename(parsed_url.pathname) + path.extname(parsed_url.pathname)
      temp_file = temp.path()
      cleanUp = !~>
        fs.unlink(temp_file)
      cleanAndLogIfErr = !(err) ~>
        if err
          console.error "Unable to import by URL.", err.stack, "URL:", url_string
        cleanUp()
      req = request.get(url_string)
      ws = fs.createWriteStream(temp_file)
      req.pipe(ws)
      ws.on 'close', !~>
        @importFile temp_file, remote_filename, cleanAndLogIfErr
      ws.on 'error', cleanAndLogIfErr
      req.on 'error', cleanAndLogIfErr


  importFile: !(temp_file, remote_filename, cb=->) ->
    tmp_with_ext = temp_file + path.extname(remote_filename)
    mv temp_file, tmp_with_ext, !(err) ~>
      return cb(err) if err
      @mpd.getFileInfo "file://#{tmp_with_ext}", !(err, track) ~>
        if err
          console.warn "Unable to read tags to get a suggested upload path: #{err.stack}"
          suggested_path = remote_filename
        else
          suggested_path = getSuggestedPath(track, remote_filename)
        relative_path = path.join('incoming', suggested_path)
        dest = path.join(@music_directory, relative_path)
        mkdirp path.dirname(dest), !(err) ~>
          if err
            console.error err
            return cb(err)
          mv tmp_with_ext, dest, !(err) ~>
            @want_to_queue.push relative_path
            @emit('state_changed')
            console.info "Track was uploaded: #{dest}"
            cb(err)

  setUpRoutes: !(app) ->
    app.post '/upload', [@checkEnabledMiddleware, multipart], !(request, response) ~>
      !~function logIfErr (err)
        if err
          console.error "Unable to import by uploading. Error: #{err}"

      for name, file in request.files
        @importFile file.path, file.name, logIfErr

      response.statusCode = 200
      response.setHeader 'content-type', 'application/json'
      response.end JSON.stringify {success: true}

  onSendStatus: !(status) ->
    @random_ids = status?.random_ids

  queueFilesPos: ->
    pos = @mpd.playlist.item_list.length
    return pos unless @random_ids?
    for item, i of @mpd.playlist.item_list
      return i if @random_ids[item.id]?

  flushWantToQueue: !->
    i = 0
    files = []
    while i < @want_to_queue.length
      file = @want_to_queue[i]
      if @mpd.library.track_table[file]?
        files.push file
        @want_to_queue.splice i, 1
      else
        i++
    @mpd.queueFiles files, @queueFilesPos()
    @mpd.queueFilesInStoredPlaylist(files, "Incoming")
    @emit('state_changed') if files.length
